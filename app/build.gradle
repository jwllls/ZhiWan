apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def DEF_APP_ANME = "智玩"
    def DEF_APPLICATION_ID = "com.hamitao.zhiwan"
    def DEF_VERSION_ANME = "1.0.0"
    def DEF_VERSION_CODE = 20
    def DEF_APP_ICON = "@drawable/app_logo"


    defaultConfig {
        //包名
        applicationId project.hasProperty("cusAppId") ? cusAppId : DEF_APPLICATION_ID
        //应用名
        resValue "string", "app_name", project.hasProperty("cusAppName") ? cusAppName : DEF_APP_ANME
        //最低版本
        minSdkVersion rootProject.ext.minSdkVersion
        //目标版本
        targetSdkVersion rootProject.ext.targetSdkVersion
        //版本号
        versionCode project.hasProperty("cusVersionCode") ? Integer.parseInt(cusVersionCode) : DEF_VERSION_CODE
        //版本名
        versionName project.hasProperty("cusVersionName") ? cusVersionName : DEF_VERSION_ANME


        ndk {
            //使用这个需要在gradle.properties添加android.useDeprecatedNdk = true
            abiFilters 'armeabi', 'armeabi-v7a'//过滤掉除armeabi文件夹以外的arm
        }

        multiDexEnabled true//解决编译时出现"finished with non-zero exit value 2"的报错,通常是因为jar包重复导致的

            manifestPlaceholders = [
                    XG_ACCESS_ID : "2100275299",
                    XG_ACCESS_KEY: "AJI226WC26AW",
            ]

        //内存泄漏配置
        configurations.all { config ->
            if (config.name.contains('UnitTest')) {
                config.resolutionStrategy.eachDependency { details ->
                    if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                        details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
                    }
                }
            }
        }
    }



    buildTypes {
        debug {
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources false
            //混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            //混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //为了兼容Android旧版本的httpClient
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //依赖注入（每个module需要单独写）
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //IM_module
    compile project(':im_module')

    //base_module
    compile project(':base_module')


}
